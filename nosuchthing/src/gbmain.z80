.macpack sm83isa
.include "gbhardware.inc"
.include "gbglobal.inc"
.export reset_handler

.segment "HRAM"
hLocals: .res locals_size

STACK_SIZE = 64
.segment "GBBSS"
stack_top: .res STACK_SIZE

.segment "CODE"
.proc reset_handler
  di  ; Disable interrupts
  ld sp, stack_top + STACK_SIZE  ; Set up stack pointer (full descending)

  ; Release the key matrix right now
  ld a,P1F_NONE
  ldh [rP1],a

  ; TODO: Set up MBC because the rest of this tech demo is in
  ; another castle 

  ; Rendering can be turned off only during blanking.
  call GB_busy_wait_vblank
  xor a
  ldh [rLCDC], a  ; turn off rendering
  ldh [rNR52], a  ; disable (and reset) audio

  ld a,IEF_VBLANK
  ldh [rIE],a  ; enable IRQs
  xor a
  ldh [rIF],a  ; acknowledge IRQs
  ei
  ldh [rSCX],a
  ldh [rSCY],a
  
  ld de,NoSuchThingAsNintendo_iu
  ld hl,CHRRAM0
  call GB_unpack_iu_file
  ld hl,SCRN_TMP
  ld de,_SCRN0 + 32
  ld bc, $130E
  call GB_load_nam

  ld a,%00011011
  ldh [rBGP],a
  ld a,LCDCF_ON|LCDCF_BG8000|LCDCF_BGON
  ldh [rLCDC],a

forever:
  halt
  jr forever
.endproc

.segment "RODATA"
NoSuchThingAsNintendo_iu:  .incbin "../obj/gb/NoSuchThingAsNintendo.iu"
