;
; Border Crossing title screen
;
; Copyright 2022 Damian Yerrick
; 
; This software is provided 'as-is', without any express or implied
; warranty.  In no event will the authors be held liable for any damages
; arising from the use of this software.
; 
; Permission is granted to anyone to use this software for any purpose,
; including commercial applications, and to alter it and redistribute it
; freely, subject to the following restrictions:
; 
; 1. The origin of this software must not be misrepresented; you must not
;    claim that you wrote the original software. If you use this software
;    in a product, an acknowledgment in the product documentation would be
;    appreciated but is not required.
; 2. Altered source versions must be plainly marked as such, and must not be
;    misrepresented as being the original software.
; 3. This notice may not be removed or altered from any source distribution.
;
include "src/hardware.inc"

section "TITLE", ROM0

show_title_screen::
  call lcd_off
  
  ld h, $80
  call clear_scrn0_to_h
  ld hl, $8800
  ld a, l
  ld c, 16
  call memset_tiny

  ; Draw the title
  ld a, $82
  ld hl, program_name
  ld de, $9800 + 32 * 1 + 3
  call Vwf16DrawStrAt
  ld hl, press_start_msg
  ld de, $9800 + 32 * 13 + 5
  call Vwf16DrawStrAt
  ld hl, copr_msg_1
  ld de, $9800 + 32 * 16 + 0
  call Vwf16DrawStrAt
  ld hl, copr_msg_2
  ld de, $9800 + 32 * 16 + 6
  call Vwf16DrawStrAt

  ; Load the pic
  ld de, title_chr
  ld hl, $9000
  ld b, 720/16
  call pb16_unpack_block
  
  ld bc, $1208  ; size
  ld hl, title_nam
  ld de, $9800 + 32 * 4 + 1
  call load_nam

  ld a, %11100100
  ldh [rBGP], a
  ld a, LCDCF_ON|LCDCF_BGON|LCDCF_BG8800
  ldh [rLCDC], a
  ld a, IEF_VBLANK
  ldh [rIE], a
  ld a, [hCapability]
  rra
  call c, sgb_unfreeze
  ei
.loop:
  call wait_vblank_irq
  call read_pad
  ldh a, [hNewKeys]
  and PADF_A|PADF_START
  jr z, .loop
  ret

press_start_msg: db "Press Start", 0
copr_msg_1: db " ", 127, " 2022", 0
copr_msg_2: db " Damian Yerrick", 0

title_chr: incbin "obj/gb/titlepic.2bpp.pb16"
title_nam: incbin "obj/gb/titlepic.nam"
